---
layout: post
title: PB -6- Arduino Bluetooth Servo kontrolÃ¼
---

BilgisayarÄ±n bluetooth Ã¶zelliÄŸini kullanarak, Arduino'ya baÄŸlÄ± servo motoru dÃ¶ndÃ¼rÃ¼yoruz. Arduino'ya program yÃ¼kledikten sonra, kablonun diÄŸer ucunu bilgisayardan Ã§Ä±karÄ±p, bir telefon ÅŸarj adaptÃ¶rÃ¼ne baÄŸlayabilirsin.<br><br>

![image](https://github.com/user-attachments/assets/ca5a272b-9441-4e3b-ac48-5c81889dffec)<br><br>

Arduino'ya bilgi yollamak iÃ§in HC-05 bluetooth modÃ¼lÃ¼nÃ¼ kullanÄ±yoruz. Ã–nceki yazÄ±da bu modÃ¼lden biraz bahsetmiÅŸtim. Bu kez Arduino'nun RX'ini, HC-05 modÃ¼lÃ¼nÃ¼n TX'ine baÄŸlÄ±yoruz. Bilgi akÄ±ÅŸÄ± tek yÃ¶nlÃ¼ olacaÄŸÄ±ndan tek tel baÄŸlamak yeterlidir. Bu projede HC-05 modÃ¼lÃ¼nÃ¼n RX pini kullanÄ±lmadÄ±ÄŸÄ±ndan direnÃ§lerle voltaj bÃ¶lmeye gerek kalmadÄ±. Burada yapacaÄŸÄ±mÄ±z iÅŸ iÃ§in bilgisayar yerine cep telefonu veya Android tablet de kullanabilirsin. Google Play'den gerekli bluetooth programÄ±nÄ± indirmen yeterli olacaktÄ±r. Motorun istenen aÃ§Ä±ya dÃ¶nmesi iÃ§in bilgi akÄ±ÅŸÄ± bu ÅŸekildedir :<br>
PureBasic (Bilgisayar) â¡ï¸ Bluetooth (Hava) â¡ï¸ HC-05 â¡ï¸ Kablo â¡ï¸ Arduino â¡ï¸ Servo motor

AÅŸaÄŸÄ±daki kodu Arduinoâ€™ya yÃ¼klÃ¼yoruz.. Ã–ncesinde, RX'e takÄ±lÄ± olan kabloyu Ã§Ä±karalÄ±m.

```c
#include <Servo.h>
Servo myservo;  
int pos = 0;    // servo position 0-180
void setup() {
  myservo.attach(9);  
  Serial.begin(19200);
}

void loop() {
  if(Serial.available()){
    pos = Serial.parseInt();
    myservo.write(pos); 
  }
  delay(20);
}
```

Bu kodu Arduino'ya yÃ¼kledikten sonra, HC-05 modÃ¼lÃ¼nden gelen kabloyu Arduino'da RX pinine takabiliriz. Yine PB-5 DHT11 yazÄ±sÄ±ndaki **comPort.pb** kodunu boÅŸ bir dizine kopyalayalÄ±m. AÅŸaÄŸÄ±daki kodu da, **servo.pb** olarak aynÄ± dizine kaydet.

```pb
XIncludeFile "comPort.pb"


Global ComData.udtComData

Enumeration EventCustomValue #PB_Event_FirstCustomValue
  #My_Event_NewData
  #My_Event_NewState
EndEnumeration

; ---------------------------------------------------------------------------

Procedure ReceiveCB(Text.s)
  PostEvent(#My_Event_NewData, 0, 0, 0, AllocateString(Text))
EndProcedure

Procedure MyEventNewDataCB()
  Protected Text.s
  Text = FreeString(EventData())
  AddGadgetItem(0, -1, Text)
  SetGadgetState(0, CountGadgetItems(0) - 1)
  SetGadgetState(0, -1)
EndProcedure

BindEvent(#My_Event_NewData, @MyEventNewDataCB())

; ---------------------------------------------------------------------------

Procedure StatusCB(Status, *ComData.udtComData)
  PostEvent(#My_Event_NewState, 0, 0, Status, *ComData)
EndProcedure

Procedure MyEventNewStateCB()
  Protected Text.s, Status, *ComData.udtComData
  Status = EventType()
  *ComData = EventData()
  Select Status
    Case #ComStatus_OpenPort
      Text = "ComStatus: Open Port " + *ComData\Port
    Case #ComStatus_ClosePort
      Text = "ComStatus: Close Port " + *ComData\Port
    Case #ComStatus_ErrorOpenPort
      Text = "ComError: Open Port " + *ComData\Port
    Case #ComStatus_ErrorSend
      Text = "ComError Send: Port " + *ComData\Port + " - " + SerialPortErrorText(*ComData\SendError)
    Case #ComStatus_ErrorReceive
      Text = "ComError Receive: Port " + *ComData\Port + " - " + SerialPortErrorText(*ComData\ReceiveError)
    Case #ComStatus_ErrorDataSize
      Text = "ComError Send: Port " + *ComData\Port + " - Send data size to big."
  EndSelect
  If Bool(Text)
    StatusBarText(0, 0, Text)
  EndIf
EndProcedure

BindEvent(#My_Event_NewState, @MyEventNewStateCB())

; ---------------------------------------------------------------------------

Procedure InitComport()
  With ComData
    If \Status
      ProcedureReturn 2 ; Always running
    EndIf
    \Port = "COM7"
    \Baud = 115200
    \Parity = #PB_SerialPort_NoParity
    \DataBit = 8
    \StopBit = 1
    \Handshake = #PB_SerialPort_NoHandshake
    \BufferSize = 2048
    \EndOfText = ""
    \StatusCB = @StatusCB()
    \ReceiveCB = @ReceiveCB()
    \ThreadID = CreateThread(@thComport(), ComData)
    If Not \ThreadID
      ProcedureReturn 0 ; Error create thread
    Else
      ProcedureReturn 1 ; ok
    EndIf
  EndWith
EndProcedure

Procedure SendToHC05(Angle)
  If ComData\Status = #ComThread_Running And ComData\SendSignal = #False
    ComData\SendText = Str(Angle)
    ComData\SendSignal = #True
  EndIf
EndProcedure


Define Event, Text.s, i
OpenWindow(0, #PB_Ignore, #PB_Ignore, 200, 200, "Servo test", 
           #PB_Window_SystemMenu | #PB_Window_ScreenCentered)
CreateStatusBar(0, WindowID(0))
AddStatusBarField(#PB_Ignore)

ButtonGadget(0, 55, 20, 90, 25, "0Â°")
ButtonGadget(1, 55, 50, 90, 25, "90Â°")
ButtonGadget(2, 55, 80, 90, 25, "180Â°")
ButtonGadget(3, 55, 130, 90, 25, "On/Off")

Repeat
  Event = WaitWindowEvent()
  Select Event
    Case #PB_Event_CloseWindow
      If ComData\Status
        MessageRequester("Info", "Comport Is Open!", #PB_MessageRequester_Warning)
      Else
        Break
      EndIf
      
    Case #PB_Event_Gadget
      Select EventGadget()
        Case 0 
          SendToHC05(0)
        Case 1 
          SendToHC05(90)
        Case 2 
          SendToHC05(180)
        Case 3 ; on/off button
          If ComData\Status
            ComData\Exit = 1
          Else
            If Not InitComport()
              StatusBarText(0, 0, "Comport " + ComData\Port + ": Error Create Thread")
            EndIf
          EndIf
      EndSelect
  EndSelect
ForEver
```

Program Ã§alÄ±ÅŸÄ±nca farkettim ki motor istenen aÃ§Ä±ya gidiyor hemen ardÄ±ndan 0 dereceye geri dÃ¶nÃ¼yor. Bu sorunun satÄ±r sonu karakterlerinden kaynaklandÄ±ÄŸÄ±nÄ± gÃ¶rdÃ¼m. InitComport() prosedÃ¼rÃ¼nde \EndOfText = "" deÄŸiÅŸkenini bu ÅŸekilde dÃ¼zeltince sorun Ã§Ã¶zÃ¼ldÃ¼. Motor dÃ¶ndÃ¼ÄŸÃ¼ aÃ§Ä±da kalÄ±yor artÄ±k ğŸï¸ Bu arada HC-05 modÃ¼lÃ¼ne bluetooth (com port) Ã¼zerinden istenen baud ile baÄŸlanÄ±labiliyor. HC-05, baud'u otomatik olarak algÄ±lÄ±yor. SG90 isminde bir servo aldÄ±m, aÄŸÄ±rlÄ±ÄŸÄ± 9 gr. fakat malesef 180 yerine 170 derece civarÄ±nda dÃ¶nÃ¼yor. Daha bÃ¼yÃ¼k servo alÄ±rsanÄ±z, direk Arduino'dan beslemek sÄ±kÄ±ntÄ± olabilir. Motoru, dÄ±ÅŸarÄ±dan 5V telefon ÅŸarj adaptÃ¶rÃ¼ ile besleyebilirsin. Bu durumda motorun GND sinyalini Arduino ile birleÅŸtirmen gerekecektir.
<br><br>
