---
layout: post
title: PB -1- Arduino ile LED kontrolÃ¼
---

Purebasic ile hazÄ±rlayacaÄŸÄ±mÄ±z, arayÃ¼z veya kullanÄ±cÄ± formu ile Arduino'yu kontrol edeceÄŸiz. Bir tane ucuzundan [Arduino Uno](https://robolinkmarket.com/arduino-uno-r3-smd-klon) alÄ±p, bu dili Ã¶ÄŸrenmek daha faydalÄ± ve eÄŸlenceli olacaktÄ±r.

Linkteki klon Arduino'yu aldÄ±ysan [CH340](https://akademi.robolinkmarket.com/ch340-driver-kurulumu) sÃ¼rÃ¼cÃ¼ kurulumunu da yapman gerekiyor. CH340, klon kartlarda usb-seri Ã§evirici entegresidir.

PC ile Arduino arasÄ±nda haberleÅŸme iÃ§in (seri baÄŸlantÄ±) Arduino'nun kendi usb kablosunu kullanacaÄŸÄ±z. Kablo dÄ±ÅŸÄ±nda bir LED ve 220 ohm ile 1K arasÄ± bir dirence ihtiyaÃ§ olacak. Benim gibi yazÄ±lÄ±ma merak sarmÄ±ÅŸ elektronikÃ§ilerin yanÄ±sÄ±ra elektroniÄŸe merak sarmÄ±ÅŸ yazÄ±lÄ±mcÄ±lar da bu yazÄ±yÄ± okuyabilir. O yÃ¼zden breadboard'dan biraz bahsedeyim. Breadboard Ã¼zerinde bulunan her 5 delik kÄ±sa devredir, malzemeleri birbirine baÄŸlamak iÃ§in kullanÄ±lÄ±r. Åemadaki gibi baÄŸlantÄ± yaparsan devre Ã§alÄ±ÅŸmayacaktÄ±r. Arduino GND'den gelen kablo, direncin girdiÄŸi 5 delikten birine girmelidir. AyrÄ±ca LED'i ters baÄŸlarsan yanmaz. Evet, bu ÅŸema iÃ§in breadboard'a gerek yok ama baÅŸka ÅŸemalarda ihtiyacÄ±n olabilir.

Seri port, yeni bir dizÃ¼stÃ¼ PC alÄ±nca gÃ¶remeyeceÄŸin, en eski baÄŸlantÄ± tipi olmasÄ±na raÄŸmen halen fabrikalarda ve yeni Ã¼retilen endÃ¼striyel cihazlarda kullanÄ±lmaktadÄ±r. En yaygÄ±n seri port soketi 2 sÄ±ra, 9 pin olandÄ±r. BaÄŸlantÄ± yapacaÄŸÄ±n cihaza gÃ¶re pin baÄŸlantÄ±larÄ± deÄŸiÅŸebilir.
<br><br>

![_config.yml]({{ site.baseurl }}/images/ledYak.jpg)<br>Ã‡alÄ±ÅŸmayan devre ÅŸemasÄ± ğŸ˜„ <br><br>

AÅŸaÄŸÄ±daki kodu Arduinoâ€™ya yÃ¼klÃ¼yoruz..<br><br>

```arduino
void setup() {
  pinMode(8, OUTPUT);
  Serial.begin(19200);
}

void loop() {
  if(Serial.available())
  {
    char c = Serial.read();
    if(c == '1')
    {
    digitalWrite(8,HIGH);
    Serial.println("LED IS ON");
    }
    else if(c == '0')
    {
    digitalWrite(8,LOW);
    Serial.println("LED IS OFF");
    }
  }
}
```
<br>
Arduino IDE'de AraÃ§lar â¡ï¸ Seri Port EkranÄ± menÃ¼sÃ¼nden, aÅŸaÄŸÄ±daki pencereyi aÃ§Ä±yoruz. Arduino'ya 1 yollarsak led yanacak, 0 yollarsak sÃ¶necektir. GÃ¶rÃ¼ldÃ¼ÄŸÃ¼ gibi Arduino bize, bilgiyi aldÄ±ÄŸÄ±nÄ± teyit eden bir cevap da yollayacaktÄ±r.<br><br>

 ![image](https://github.com/user-attachments/assets/8cefbe0b-5e05-4e66-a6c8-0383dfc46dbf)
<br><br>

>Bu iÅŸleri yapmak iÃ§in [Arduino IDE 1.8.19](https://downloads.arduino.cc/arduino-1.8.19-windows.zip) sÃ¼rÃ¼mÃ¼nÃ¼ indirdim. (Gerekenden daha bÃ¼yÃ¼k yazÄ±lÄ±mlarÄ± bilgisayara yÃ¼kleyerek, onu kastÄ±rmaya oldum olasÄ± karÅŸÄ± Ã§Ä±kmÄ±ÅŸÄ±mdÄ±r. O yÃ¼zden Ä±srarla, son sÃ¼rÃ¼m olan 2.3.2'yi indirmiyorum.) PureBasic son sÃ¼rÃ¼m Demo programÄ±nÄ±, [buradan](https://www.purebasic.com/download.php) indirebilirsin.. BilgisayarÄ±n, Windows ise 64-bit sÃ¼rÃ¼mÃ¼ indirebilirsin.. Tabii, Mac OS veya Linux kullanÄ±yorsan, sana uygun sÃ¼rÃ¼mÃ¼ indirebilirsin. PB kurulum dosyasÄ±, Arduino IDE'den daha kÃ¼Ã§Ã¼k farkettiysen. PB Demo ile en fazla 800 satÄ±r bÃ¼yÃ¼klÃ¼ÄŸÃ¼nde bir program yazabilirsin. Bu da, bu tip projeler iÃ§in gayet yeterli bir satÄ±r sayÄ±sÄ±dÄ±r..

<br>

Bu Arduino kodu, ne iÅŸe yarayacak, Ã§ok basit demeyin. Burada amacÄ±mÄ±z PB'i tanÄ±mak olsa da bu kodu bir projede kullanmÄ±ÅŸtÄ±k. KonveyÃ¶r bantta ilerleyen Ã¼rÃ¼nlerin Ã¼zerine etiketleme iÃ§in yaz ve uygula makinasÄ± yerleÅŸtirmiÅŸtik. Makinadan geÃ§en, etiketi uygulanan Ã¼rÃ¼nler sonrasÄ±nda bir barkod okuyucunun altÄ±ndan geÃ§iyordu. Barkod okununca, bu bilgi PC'de bulunan SQL sunucuya kaydediliyordu. Barkod'un okunamamasÄ± durumunda, konveyÃ¶rÃ¼n durmasÄ± gerekir. Ä°ÅŸte bu noktada, Arduino'ya ÅŸemadaki led yerine bir SSR baÄŸlamÄ±ÅŸtÄ±m. SSR Ã§Ä±kÄ±ÅŸÄ±nÄ± da konveyÃ¶r motorunun sÃ¼rÃ¼cÃ¼sÃ¼ne baÄŸladÄ±k. Bu sayede, PC'de yazÄ±lÄ±m geliÅŸtiren arkadaÅŸ konveyÃ¶rÃ¼ durdurup, harekete geÃ§irebiliyordu. SSR, yerine bir rÃ¶le de baÄŸlanabilirdi. Bu tip iÅŸlerde, normalde PLC kullanÄ±lÄ±r.. Bir tane kontak iÃ§in de PLC'ye gerek yok diye dÃ¼ÅŸÃ¼nmÃ¼ÅŸtÃ¼m.

AÅŸaÄŸÄ±daki kodu PB IDEâ€™ye kopyalayÄ±p yapÄ±ÅŸtÄ±rÄ±n.. Ã–ncesinde **New** butonuna basarak boÅŸ bir sayfa aÃ§Ä±n. ArtÄ±k Arduino IDE'yi kapatabilirsin. Ã‡Ã¼nkÃ¼, seri portu, aynÄ± anda ancak bir program kullanabilir..  <br><br>

```pb
EnableExplicit

Enumeration
  #win_main
  #txt
  #txt_status
  #btn_on
  #btn_off
  #cmb_comPort
  #serial_portNr
EndEnumeration

Global PortsFound = 0, Event, COMPort.s="", DataReceived.s
Global StartTime, Timeout = 1000, Result
Global *Buffer = AllocateMemory(128)

#FLAGS = #PB_Window_SystemMenu | #PB_Window_ScreenCentered
OpenWindow(#win_main, 0, 0, 200, 180, "Arduino LED control", #FLAGS) 
ComboBoxGadget(#cmb_comPort, 110, 15, 80, 23)

TextGadget(#txt, 10, 25, 280, 20, "Serial Port :")
TextGadget(#txt_status, 0, 140, 200, 40, "Status comes from Arduino", #PB_Text_Center)
ButtonGadget(#btn_on, 10, 70, 80, 50, "LED ON")
ButtonGadget(#btn_off, 110, 70, 80, 50, "LED OFF")


Procedure Find_Ports()
  Protected i
  For i = 0 To 255
    COMPort="COM"+Str(i+1)
    If OpenSerialPort(i, COMPort, 19200, 0, 8, 1, #PB_SerialPort_NoHandshake, 1, 1)        
      AddGadgetItem(#cmb_comPort,-1, COMPort)
      CloseSerialPort(i) 
    EndIf
  Next
  PortsFound=1
EndProcedure


Procedure Port_Control()
  If  IsSerialPort(#serial_portNr) : CloseSerialPort(#serial_portNr) : EndIf
  
  COMPort = GetGadgetText(#cmb_comPort)
  If COMPort = ""
    SetGadgetText(#txt_status, "First select port !")
    ProcedureReturn #False
  Else
    If IsSerialPort(#serial_portNr); if port open
      ProcedureReturn #True
    Else                            ; if port closed
      OpenSerialPort(#serial_portNr, COMPort, 19200, 0, 8, 1, #PB_SerialPort_NoHandshake, 1, 1)
      SetGadgetText(#txt_status, "Port is open.. Wait 2-3 secs" + #LF$ + 
                                 "for the arduino to be ready..")
      
      If IsSerialPort(#serial_portNr)
        ProcedureReturn #True
      Else
        MessageRequester("Error","Port can't be opened !", #PB_MessageRequester_Error)
        ProcedureReturn #False
      EndIf 
    EndIf     
  EndIf
EndProcedure


Procedure Receive_Response()
  Repeat
    Event = WaitWindowEvent(20)
    Result = AvailableSerialPortInput(#serial_portNr)      
    If Result 
      ;While Result 
      ReadSerialPortData(#serial_portNr, *Buffer, Result) 
      DataReceived = PeekS(*Buffer, -1, #PB_Ascii)
      ;Result = AvailableSerialPortInput(#serial_portNr) 
      ;Wend
      SetGadgetText(#txt_status, DataReceived)
      Break 
    EndIf
    Delay(1)  ; Let system do other things 
  Until ElapsedMilliseconds() - StartTime > Timeout
  
  If ElapsedMilliseconds() - StartTime > Timeout
    MessageRequester("Error","Usb cable is disconnected or.." + 
                             #LF$ + "Arduino isn't connected to the selected port or.." + 
                             #LF$ + "You didn't wait 2-3 seconds when the port was first opened..",
                     #PB_MessageRequester_Error)
    CloseSerialPort(#serial_portNr)
  EndIf                 
EndProcedure


Repeat
  Event = WaitWindowEvent()
  
  If PortsFound=0
    Find_Ports()
  EndIf
  
  Select Event
    Case #PB_Event_Gadget
      Select EventGadget()
          
        Case #btn_on
          If IsSerialPort(#serial_portNr)
            WriteSerialPortString(#serial_portNr, "1", #PB_Ascii)
            StartTime = ElapsedMilliseconds()
            Receive_Response()
          Else 
            Port_Control()
          EndIf  
          
        Case #btn_off 
          If IsSerialPort(#serial_portNr)
            WriteSerialPortString(#serial_portNr, "0", #PB_Ascii)
            StartTime = ElapsedMilliseconds()
            Receive_Response()
          Else 
            Port_Control()
          EndIf 
          
        Case #cmb_comPort
          Port_Control()
          
      EndSelect
  EndSelect
Until Event = #PB_Event_CloseWindow

FreeMemory(*Buffer)
End

```
<br>

[Youtube'da izleyebilirsin..](https://youtu.be/6pM-lTXhI9Q)<br>Yorum veya gÃ¶rÃ¼ÅŸlerini Youtube yorumlarÄ±na yazabilirsin..<br><br>

ProgramÄ± test etmek iÃ§in aslÄ±nda Arduino'ya da ihtiyacÄ±n yok. Bu durumda Arduino'nun yaptÄ±ÄŸÄ± iÅŸi senin yapman gerekecek. Eski blog sayfamda anlattÄ±ÄŸÄ±m [com0com](https://erolcum.blogspot.com/2022/12/null-modem-emulator-com0com-sanal-seri.html) sanal seri port programÄ±nÄ± kullanabilirsin.<br><br>

ProgramÄ± Ã§alÄ±ÅŸtÄ±rÄ±nca kÃ¼Ã§Ã¼k bir pencere aÃ§Ä±lacaktÄ±r..<br><br>
![image](https://github.com/user-attachments/assets/3b7ad436-8114-4a26-b091-18558240533f)<br><br>

Herhangi bir komutla ilgili bilgi almak istersen, o komutun Ã¼stÃ¼ne tÄ±kla.. Klavyeden F1 tuÅŸuna bastÄ±ÄŸÄ±nda yardÄ±m sayfasÄ± aÃ§Ä±lacaktÄ±r. Kodlardan biraz bahsedelim. Enumeration bÃ¶lÃ¼mÃ¼nde bize ait sabitleri tanÄ±mlÄ±yoruz. Bize ait dedim, Ã§Ã¼nkÃ¼ #PB_ ile baÅŸlayan sabitler PB'e aittir. PB'e ait sabitleri ve yapÄ±larÄ± (structure) gÃ¶rmek iÃ§in Tools â¡ï¸ Structure Viewer menÃ¼sÃ¼ne tÄ±klayabilirsin. Kodumuzda, en Ã¼stteki sabit 0, en alttaki 6 deÄŸerini almÄ±ÅŸ oluyor. Bu sabitleri, gadget'leri kimliklendirmede kullanÄ±yoruz. Onun altÄ±nda pencereyi ve gadget'leri oluÅŸturuyoruz. Formu bu ÅŸekilde kodlarla hazÄ±rlamak daha Ã§ok hoÅŸuma gidiyor. BÃ¶ylece herÅŸey tek sayfada gÃ¶zÃ¼kÃ¼yor. Find_Ports() prosedÃ¼rÃ¼, PC'de bulunan seri portlarÄ± bulup ComboBox'a kaydediyor. Port_Control() prosedÃ¼rÃ¼, kullanÄ±cÄ± port deÄŸiÅŸtirince Ã§alÄ±ÅŸÄ±yor. Receive_Response(), butona tÄ±klayÄ±nca Arduino cevap verecektir, bu cevapÄ± okumak iÃ§in kullanÄ±lan prosedÃ¼rdÃ¼r. Repeat-Until gibi dÃ¶ngÃ¼lerde, pencere iÃ§in oluÅŸan olaylarÄ±n sifonunu Ã§ekmeniz gerekir. (Ä°ngilizcesi flush olan kelimenin TÃ¼rkÃ§esi sifon ğŸ˜ƒ) Aksi halde pencere donacak, yanÄ±t vermeyecektir. Bu amaÃ§la burada WaitWindowEvent(20) komutu kullanÄ±ldÄ±. Bu komuta parametre vermezsek, program bir olay oluÅŸana kadar bu komutta bekler. Parantez iÃ§inde 20 yazdÄ±m ki, beklemesin, 20 milisaniye sonra devam etsin.. GerÃ§i buradaki dÃ¶ngÃ¼ kÄ±sa ama uzun bir dÃ¶ngÃ¼ iÃ§inde, Delay(1) gibi bir komut kullanmanÄ±z gerekiyor. Bu da CPU'i fazla meÅŸgÃ¼l etmemek iÃ§in gerekli. SanÄ±rÄ±m, ÅŸimdilik bu kadar tiyo yeterli olacaktÄ±r. ğŸ˜µâ€ğŸ’« DediÄŸim gibi, Youtube yorumlarÄ±na, gÃ¶rÃ¼ÅŸ veya sorularÄ±nÄ± yazmaktan Ã§ekinme lÃ¼tfen.




