---
layout: post
title: Arduino ve PureBasic Haberleşmesi 1 - LED YAKMA
---

Herkese merhabalar 👋 Projelerinizi bilgisayar üzerinden kendi tasarladığınız bir arayüzle kontrol etmek ister misiniz ? Bu yazıda sizlerle yeni bir seriye başlıyoruz. **Seri haberleşme** yardımıyla ve **PureBasic** kullanarak oluşturduğumuz arayüzlerle projelerimizi kontrol etmeyi ve veri okumayı öğreneceğiz. Geliştirdiğimiz arayüzlerle Arduino projelerimizi ve PB öğrenme serüvenini çok daha eğlenceli hale getireceğiz. 

En basit tanımıyla seri iletişim dijital bilginin tek bir hat üzerinden ardı sıra taşınmasıdır, yani seri iletişimde bir kerede sadece bir bit iletilir, 1 veya 0..<br><br>

![_config.yml]({{ site.baseurl }}/images/ledYak.jpg)<br>Çalışmayan devre şeması 😄 <br><br>

Aşağıdaki kodu Arduino’ya yüklüyoruz..<br><br>

```arduino
void setup() {
  pinMode(8, OUTPUT);
  Serial.begin(19200);
}

void loop() {
  if(Serial.available())
  {
    char c = Serial.read();
    if(c == '1')
    {
    digitalWrite(8,HIGH);
    Serial.println("LED IS ON");
    }
    else if(c == '0')
    {
    digitalWrite(8,LOW);
    Serial.println("LED IS OFF");
    }
  }
}
```
<br>
Arduino IDE'de Araçlar ➡️ Seri Port Ekranı menüsünden, aşağıdaki pencereyi açıyoruz. Arduino'ya 1 yollarsak led yanacak, 0 yollarsak sönecektir.<br><br>

 ![image](https://github.com/user-attachments/assets/8cefbe0b-5e05-4e66-a6c8-0383dfc46dbf)
<br><br>

>Bu işleri yapmak için [Arduino IDE 1.8.19](https://downloads.arduino.cc/arduino-1.8.19-windows.zip) sürümünü indirdim. (Gerekenden daha büyük yazılımları bilgisayara yükleyerek, onu kastırmaya oldum olası karşı çıkmışımdır. O yüzden ısrarla, son sürüm olan 2.3.2'yi indirmiyorum.) PureBasic son sürüm Demo programını, gönül rahatlığıyla [buradan](https://www.purebasic.com/download.php) indirebilirsiniz.. Bilgisayarınız, Windows ise 64-bit sürümü indirebilirsiniz.. Tabii, Mac OS veya Linux kullanıyorsanız, uygun sürümü indiriniz. PB kurulum dosyası, Arduino IDE'den daha küçük farkettiyseniz. PB Demo ile en fazla 800 satır büyüklüğünde bir program yazabilirsiniz. Bu da, bu tip projeler için gayet yeterli bir satır sayısıdır..

<br>

Bu Arduino kodu, ne işe yarayacak, çok basit demeyin. Burada amacımız PB'i tanımak olsa da bu kodu bir projede kullanmıştık. Konveyör bantta ilerleyen ürünlerin üzerine etiketleme için yaz ve uygula makinası yerleştirmiştik. Makinadan geçen, etiketi uygulanan ürünler sonrasında bir barkod okuyucunun altından geçiyordu. Barkod okununca, bu bilgi pc'de bulunan SQL sunucuya kaydediliyordu. Barkod'un okunamaması durumunda, konveyörün durması gerekir. İşte bu noktada, Arduino'ya şemadaki led yerine bir SSR bağlamıştım. SSR çıkışını da konveyör motorunun sürücüsüne bağladık. Bu sayede, pc'de yazılım geliştiren arkadaş konveyörü durdurup, harekete geçirebiliyordu. SSR, yerine bir röle de bağlanabilirdi. Bu tip işlerde, normalde PLC kullanılır.. Bir tane kontak için de PLC'ye gerek yok diye düşünmüştüm.

Aşağıdaki kodu PB IDE’ye kopyalayıp yapıştırın.. Öncesinde **New** butonuna basarak boş bir sayfa açın. Artık Arduino IDE'yi kapatabilirsiniz. Çünkü, seri portu, aynı anda ancak bir program kullanabilir..  <br><br>

```pb
EnableExplicit

Enumeration
  #win_main
  #txt
  #txt_status
  #btn_on
  #btn_off
  #cmb_comPort
  #serial_port_nr
EndEnumeration

Global PortsFound = 0, Event, COMPort.s="", DataReceived.s
Global StartTime, Timeout = 1000, Result
Global *Buffer = AllocateMemory(128)

#FLAGS = #PB_Window_SystemMenu | #PB_Window_ScreenCentered
OpenWindow(#win_main, 0, 0, 200, 180, "Arduino LED control", #FLAGS) 
ComboBoxGadget(#cmb_comPort, 110, 15, 80, 23)

TextGadget(#txt, 10, 25, 280, 20, "Serial Port :")
TextGadget(#txt_status, 0, 140, 200, 40, "Status comes from Arduino", #PB_Text_Center)
ButtonGadget(#btn_on, 10, 70, 80, 50, "YAK")
ButtonGadget(#btn_off, 110, 70, 80, 50, "LED OFF")


Procedure Find_Ports()
  Protected i
  For i = 0 To 255
    COMPort="COM"+Str(i+1)
    If OpenSerialPort(i, COMPort, 19200, 0, 8, 1, #PB_SerialPort_NoHandshake, 1, 1)        
      AddGadgetItem(#cmb_comPort,-1, COMPort)
      CloseSerialPort(i) 
    EndIf
  Next
  PortsFound=1
EndProcedure


Procedure Port_Control()
  If  IsSerialPort(#serial_port_nr) : CloseSerialPort(#serial_port_nr) : EndIf
  
  COMPort = GetGadgetText(#cmb_comPort)
  If COMPort = ""
    SetGadgetText(#txt_status, "First select port !")
    ProcedureReturn #False
  Else
    If IsSerialPort(#serial_port_nr); if port open
      ProcedureReturn #True
    Else                            ; if port closed
      OpenSerialPort(#serial_port_nr, COMPort, 19200, 0, 8, 1, #PB_SerialPort_NoHandshake, 1, 1)
      SetGadgetText(#txt_status, "Port is open.." + #LF$ + "Wait 2-3 secs..")
      
      If IsSerialPort(#serial_port_nr)
        ProcedureReturn #True
      Else
        MessageRequester("Error","Port can't be opened !", #PB_MessageRequester_Error)
        ProcedureReturn #False
      EndIf 
    EndIf     
  EndIf
EndProcedure


Procedure Receive_Response()
  Repeat
    Event = WaitWindowEvent(20)
    Result = AvailableSerialPortInput(#serial_port_nr)      
    If Result 
      ;While Result 
      ReadSerialPortData(#serial_port_nr, *Buffer, Result) 
      DataReceived = PeekS(*Buffer, -1, #PB_Ascii)
      ;Result = AvailableSerialPortInput(#serial_port_nr) 
      ;Wend
      SetGadgetText(#txt_status, DataReceived)
      Break 
    EndIf
    Delay(1)  ; Let system do other things 
  Until ElapsedMilliseconds() - StartTime > Timeout
  
  If ElapsedMilliseconds() - StartTime > Timeout
    MessageRequester("Error","Usb cable is disconnected or.." + 
                             #LF$ + "Arduino isn't connected to the selected port or.." + 
                             #LF$ + "You didn't wait 2-3 seconds when the port was first opened..",
                     #PB_MessageRequester_Error)
    CloseSerialPort(#serial_port_nr)
  EndIf                 
EndProcedure


Repeat
  Event = WaitWindowEvent()
  
  If PortsFound=0
    Find_Ports()
  EndIf
  
  Select Event
    Case #PB_Event_Gadget
      Select EventGadget()
          
        Case #btn_on
          If IsSerialPort(#serial_port_nr)
            WriteSerialPortString(#serial_port_nr, "1", #PB_Ascii)
            StartTime = ElapsedMilliseconds()
            Receive_Response()
          Else 
            Port_Control()
          EndIf  
          
        Case #btn_off 
          If IsSerialPort(#serial_port_nr)
            WriteSerialPortString(#serial_port_nr, "0", #PB_Ascii)
            StartTime = ElapsedMilliseconds()
            Receive_Response()
          Else 
            Port_Control()
          EndIf 
          
        Case #cmb_comPort
          Port_Control()
          
      EndSelect
  EndSelect
Until Event = #PB_Event_CloseWindow

FreeMemory(*Buffer)
End

```
<br>

[Youtube'da izleyebilirsin..](https://youtu.be/6pM-lTXhI9Q)


